import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Category.Cat
import Lean4.directed_path_homotopy
import Mathlib.AlgebraicTopology.FundamentalGroupoid.Basic

/-
  This file contains the definition of the fundamental category of a directed space.
  We follow the structure of the undirected version found at:
  https://leanprover-community.github.io/mathlib_docs/algebraic_topology/fundamental_groupoid/basic.html#fundamental_groupoid
-/

open DirectedMap
open CategoryTheory

universe u v
variable {X : Type u} {Y : Type v} [DirectedSpace X] [DirectedSpace Y] {xâ‚€ xâ‚ : X}

open scoped unitInterval

noncomputable section

namespace Dipath

namespace Dihomotopy

open Path.Homotopy

section assoc

lemma transAssocReparamAux_directed : DirectedMap.Directed
  ({ toFun := fun t => âŸ¨transAssocReparamAux t, transAssocReparamAux_mem_I tâŸ©} : C(I, I)) := by
  apply DirectedUnitInterval.directed_of_monotone _
  intros x y hxy
  unfold transAssocReparamAux
  simp
  have : (x : â„) â‰¤ (y : â„) := hxy
  split_ifs with hâ‚€ hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ hâ‚…
  Â· linarith
  Â· linarith
  Â· push_neg at hâ‚‚
    have : 0 â‰¤ (y : â„) := le_trans (by norm_num) (le_of_lt hâ‚‚)
    have : 1 â‰¤ (y : â„) + 1 := by linarith
    calc 2 * (x : â„)
      _ â‰¤ 2 * 4â»Â¹ := (mul_le_mul_left (by norm_num)).mpr hâ‚€
      _ = (2â»Â¹ : â„) * 1 := by norm_num
      _ â‰¤ (2â»Â¹ : â„) * (â†‘y + 1) := (mul_le_mul_left (by norm_num)).mpr this
  Â· linarith
  Â· linarith
  Â· push_neg at hâ‚…
    calc (x : â„) + (4â»Â¹ : â„)
      _ â‰¤ 2â»Â¹ + 4â»Â¹ := add_le_add_right hâ‚ƒ 4â»Â¹
      _ = 2â»Â¹ * (2â»Â¹ + 1) := by norm_num
      _ â‰¤ 2â»Â¹ * (â†‘y + 1) := (mul_le_mul_left (by norm_num)).mpr (add_le_add_right (le_of_lt hâ‚…) 1)
  Â· linarith
  Â· linarith
  Â· apply (mul_le_mul_left (show 0 < (2â»Â¹ : â„) by norm_num)).mpr
    apply add_le_add_right
    exact hxy

def transAssocReparamAuxMap : D(I, I) where
  toFun := fun t => âŸ¨transAssocReparamAux t, transAssocReparamAux_mem_I tâŸ©
  directed_toFun := transAssocReparamAux_directed

lemma trans_assoc_reparam_directed {xâ‚€ xâ‚ xâ‚‚ xâ‚ƒ : X} (p : Dipath xâ‚€ xâ‚) (q : Dipath xâ‚ xâ‚‚) (r : Dipath xâ‚‚ xâ‚ƒ) :
    (p.trans q).trans r = (p.trans (q.trans r)).reparam
      transAssocReparamAuxMap
      (Subtype.ext transAssocReparamAux_zero)
      (Subtype.ext transAssocReparamAux_one) := by
  ext t
  have : (p.trans q).trans r t =  (p.toPath.trans q.toPath).trans r.toPath t := rfl
  rw [this, trans_assoc_reparam p.toPath q.toPath r.toPath]
  rfl

/--
For any three dipaths `p q r`, `(p.trans q).trans r` is dihomotopic with `p.trans (q.trans r)`.
-/
def trans_assoc {xâ‚€ xâ‚ xâ‚‚ xâ‚ƒ : X} (p : Dipath xâ‚€ xâ‚) (q : Dipath xâ‚ xâ‚‚) (r : Dipath xâ‚‚ xâ‚ƒ) :
    ((p.trans q).trans r).Dihomotopic (p.trans (q.trans r)) := by
  have := Dihomotopic.reparam (p.trans (q.trans r)) transAssocReparamAuxMap
    (Subtype.ext transAssocReparamAux_zero)
    (Subtype.ext transAssocReparamAux_one)
  rw [â†trans_assoc_reparam_directed] at this
  exact EqvGen.symm _ _ this

end assoc

end Dihomotopy

end Dipath

/-
 Definition of the fundamental category and of the functor sending a directed space to its
 fundamental category
-/
@[ext]
structure FundamentalCategory (X : Type u) where
  as : X

namespace FundamentalCategory

@[simps]
def equiv (X : Type*) : FundamentalCategory X â‰ƒ X where
  toFun x := x.as
  invFun x := .mk x
  left_inv _ := rfl
  right_inv _ := rfl

@[simp]
lemma isEmpty_iff (X : Type*) :
    IsEmpty (FundamentalCategory X) â†” IsEmpty X :=
  equiv _ |>.isEmpty_congr

instance (X : Type*) [IsEmpty X] :
    IsEmpty (FundamentalCategory X) :=
  equiv _ |>.isEmpty

@[simp]
lemma nonempty_iff (X : Type*) :
    Nonempty (FundamentalCategory X) â†” Nonempty X :=
  equiv _ |>.nonempty_congr

instance (X : Type*) [Nonempty X] :
    Nonempty (FundamentalCategory X) :=
  equiv _ |>.nonempty

@[simp]
lemma subsingleton_iff (X : Type*) :
    Subsingleton (FundamentalCategory X) â†” Subsingleton X :=
  equiv _ |>.subsingleton_congr

instance (X : Type*) [Subsingleton X] :
    Subsingleton (FundamentalCategory X) :=
  equiv _ |>.subsingleton


instance {X : Type u} [Inhabited X] : Inhabited (FundamentalCategory X) :=
  âŸ¨âŸ¨defaultâŸ©âŸ©

attribute [local instance] Dipath.Dihomotopic.setoid

instance : CategoryTheory.Category (FundamentalCategory X) where
  Hom x y := Dipath.Dihomotopic.Quotient x.as y.as
  id x := âŸ¦Dipath.refl x.asâŸ§
  comp {_ _ _} := Dipath.Dihomotopic.Quotient.comp
  id_comp {x _} f :=
    Quotient.inductionOn f fun a =>
      show âŸ¦(Dipath.refl x.as).trans aâŸ§ = âŸ¦aâŸ§ from Quotient.sound (EqvGen.rel _ _ âŸ¨Dipath.Dihomotopy.refl_trans aâŸ©)
  comp_id {_ y} f :=
    Quotient.inductionOn f fun a =>
      show âŸ¦a.trans (Dipath.refl y.as)âŸ§ = âŸ¦aâŸ§ from Quotient.sound (EqvGen.symm _ _ (EqvGen.rel _ _ âŸ¨Dipath.Dihomotopy.trans_refl aâŸ©))
  assoc {_ _ _ _} f g h :=
    Quotient.inductionOnâ‚ƒ f g h fun p q r =>
      show âŸ¦(p.trans q).trans râŸ§ = âŸ¦p.trans (q.trans r)âŸ§ from
        Quotient.sound (Dipath.Dihomotopy.trans_assoc p q r)

lemma comp_eq (x y z : FundamentalCategory X) (p : x âŸ¶ y) (q : y âŸ¶ z) :
    p â‰« q = p.comp q := rfl

lemma id_eq_path_refl (x : FundamentalCategory X) :
    ğŸ™ x = âŸ¦Dipath.refl x.asâŸ§ := rfl

def fundamentalCategoryFunctor : dTopCat â¥¤ CategoryTheory.Cat where
  obj X := { Î± := FundamentalCategory X }
  map f :=
    { /- Functor from Î (X) to Î (Y) -/
      obj := fun x => âŸ¨f x.asâŸ©
      map := fun {X Y} p => by exact p.mapFn f
      map_id := fun X => rfl
      map_comp := fun {x y z} p q => by
        refine Quotient.inductionOnâ‚‚ p q fun a b => ?_
        simp only [comp_eq, â†Dipath.Dihomotopic.map_lift, â†Dipath.Dihomotopic.comp_lift, Dipath.map_trans]
        erw [â†Dipath.Dihomotopic.comp_lift]; rfl
    }

  map_id X := by
    simp only
    change _ = (âŸ¨_, _, _âŸ© : FundamentalCategory X â¥¤ FundamentalCategory X)
    congr
    ext x y p
    refine' Quotient.inductionOn p fun q => _
    rw [â† Dipath.Dihomotopic.map_lift]
    conv_rhs => rw [â†q.map_id]

  map_comp f g := by
    simp only
    congr
    ext x y p
    refine' Quotient.inductionOn p fun q => _
    simp only [Quotient.map_mk, Dipath.map_map, Quotient.eq']
    rfl

scoped notation "dÏ€" => FundamentalCategory.fundamentalCategoryFunctor
scoped notation "dÏ€â‚“" => FundamentalCategory.fundamentalCategoryFunctor.obj
scoped notation "dÏ€â‚˜" => FundamentalCategory.fundamentalCategoryFunctor.map

lemma map_eq {X Y : dTopCat} {xâ‚€ xâ‚ : X} (f : D(X, Y)) (p : Dipath.Dihomotopic.Quotient xâ‚€ xâ‚) :
  (dÏ€â‚˜ f).map p = p.mapFn f := rfl

/-- Help the typechecker by converting a point in the fundamental category back to a point in
the underlying directed space. -/
@[reducible]
def toTop {X : dTopCat} (x : dÏ€â‚“ X) : X := x.as

/-- Help the typechecker by converting a point in a directed space to a
point in the fundamental category of that space -/
@[reducible]
def fromTop {X : dTopCat} (x : X) : dÏ€â‚“ X := âŸ¨xâŸ©

/-- Help the typechecker by converting an arrow in the fundamental category of
a directed space back to a directed path in that space (i.e., `Dipath.Dihomotopic.Quotient`). -/
@[reducible]
def toPath {X : dTopCat} {xâ‚€ xâ‚ : dÏ€â‚“ X} (p : xâ‚€ âŸ¶ xâ‚) :
  Dipath.Dihomotopic.Quotient (X := X) xâ‚€.as xâ‚.as := p

/-- Help the typechecker by convering a directed path in a directed space to an arrow in the
fundamental category of that space. -/
@[reducible]
def fromPath {X : dTopCat} {xâ‚€ xâ‚ : X} (p : Dipath.Dihomotopic.Quotient xâ‚€ xâ‚) :
  FundamentalCategory.mk xâ‚€ âŸ¶ FundamentalCategory.mk xâ‚ := p

end FundamentalCategory
